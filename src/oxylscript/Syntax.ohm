OxylScript {
	Program = SourceElement*
	SourceElement = (Statement
					| Loop
					| Effect
                    | Expression
                    | Condition
					 | AnyType ) semi?

	Block = "{" SourceElement* "}"
 
	space := whitespace | lineTerminator | comment

	whitespace = "\t"
				| "\x0B" -- verticalTab
				| "\x0C" -- formFeed
				| " "
				| "\u00A0" -- noBreakSpace
				| "\uFEFF"  -- byteOrderMark
				| "\u2000".."\u200B"
				| "\u3000"

	semi = ";"
 	lineTerminator = "\n" | "\r" | "\u2028" | "\u2029"
	lineTerminatorSequence = "\n" | "\r" ~"\n" | "\u2028" | "\u2029" | "\r\n"

	comment = multiLineComment | singleLineComment

	multiLineComment = "/*" (~"*/" any)* "*/"
 	singleLineComment = "//" (~lineTerminator any)*
 
	AnyType = Boolean
			| Permission
			| Integer
			| Number
			| User
			| Member
			| Message
			| Channel
			| Role
			| Guild
			| String
			| Variable

	// Priority

	BooleanPriority = "(" Boolean ")"
	PermissionPriority = "(" Permission ")"
	IntegerPriority = "(" Integer ")"
	NumberPriority = "(" Number ")"
	UserPriority = "(" User ")"
	MemberPriority = "(" Member ")"
	MessagePriority = "(" Message ")"
	ChannelPriority = "(" Channel ")"
	RolePriority = "(" Role ")"
	GuildPriority = "(" Guild ")"
	StringPriority = "(" String ")"
	VariablePriority = "(" Variable ")"

	// Expressions
	Expression = AbsoluteValue
				| Arithmetic
				| CleanContent
				| CreationTimestamp
				| Difference
				| FormatDate
				| ID
				| IndexOf
				| MemberCount
				| MemberGame
				| MemberJoinTimestamp
				| MemberStatus
				| Mention
				| MessageAuthor
				| MessageContent
				| Name
				| RandomNumber
				| Rounded
				| RoundedDown
				| RoundedUp
				| GuildIcon
				| GuildRegion
				| GuildOwner
				| Substring
				| Concat
				| Length
				| Lowercase
				| Uppercase
				| UserAvatar
				| UserDiscriminator
				| UserTag
				| UserFromID
				| MemberFromUser
				| MemberFromID
				| ChannelFromID
				| RoleFromID
				| RoleFromName

	AbsoluteValue = ("absolute" | "abs") "value"? "of" Number -- abs
					| "|" Number "|" -- pipes

	Arithmetic = Addition

	Addition = Addition "+" Multiplication -- plus
			| Addition "-" Multiplication -- minus
			| Multiplication
			
	Multiplication = Multiplication "*" Exponent -- times
					| Multiplication "/" Exponent -- divide
					| Exponent
		
	Exponent = Number "^" Exponent -- power
			| Number

	CleanContent = "clean content of" Message -- messageLast
				| Message "'s clean content" -- messageFirst

	CreationTimestamp = "creation timestamp of" DiscordType -- objectLast
				| DiscordType "'s creation timestamp'" -- objectFirst

	Difference = "difference between" Number "and" Number

	FormatDate = Number "formatted as date"

	ID = "id of" DiscordType -- objectLast
		| DiscordType "'s id" -- objectFirst

	IndexOf = "index of" String "in" String

	MemberCount = "member count of" Guild -- guildLast
				| Guild "'s member count" -- guildFirst

	MemberGame = "game of" Member -- memberLast
				| Member "'s game" -- memberFirst

	MemberJoinTimestamp = "join timestamp of" Member -- memberLast
						| Member "'s join timestamp" -- memberFirst

	MemberStatus = "status of" Member -- memberLast
				| Member "'s status" -- memberFirst

	Mention = "mention of" Mentionable -- mentionableLast
			| Mentionable "'s mention" -- mentionableFirst

	MessageAuthor = "author of" Message -- messageLast
					| Message "'s author" -- messageFirst

	MessageContent = "content of" Message -- messageLast
					| Message "'s content" -- messageFirst

	Name = "name of" HasName -- objectLast
		| HasName "'s name" -- objectFirst

	Username = "username of" User -- userLast
		| User "'s username" -- userFirst

	RandomNumber = "random number between" Integer "to" Integer -- exclusive
				| "random number between" Integer "to" Integer "inclusive" -- inclusive

	Rounded = Number "rounded"

	RoundedDown = Number ("rounded down" | "floor")

	RoundedUp = Number ("rounded up" | "ceil")

	GuildIcon = "icon of" Guild -- guildLast
				| Guild "'s icon" -- guildFirst

	GuildRegion = "region of" Guild -- guildLast
				| Guild "'s region" -- guildFirst

	GuildOwner = "owner of" Guild --guildLast
				| Guild "'s owner" -- guildFirst

	Substring = SubstringFromIndices
				| SubstringFirstCharacters
				| SubstringLastCharacters

	SubstringFromIndices = "the"? "part of" String ("between" | "from") Integer "to" Integer
						| "the"? "substring of" String ("between" | "from") Integer "to" Integer

	SubstringFirstCharacters = "the"? "first" Integer "characters of" String

	SubstringLastCharacters = "the"? "last" Integer "characters of" String

	Concat = String "+" AnyType
			| AnyType "+" String
			| String "added to" AnyType
			| AnyType "added to" String

	Length = "length of" String -- stringLast
			| String "'s length" -- stringFirst

	Lowercase = "lowercase of" String

	Uppercase = "uppercase of" String

	UserAvatar = "avatar of" User -- userLast
				| User "'s avatar" -- userFirst

	UserDiscriminator = "discriminator of" User -- userLast
						| User "'s discriminator" -- userFirst

	UserTag = "tag of" User -- userLast
			| User "'s tag" -- userFirst

	UserFromID = "the"? "user" ("from" | "with" | "of") "id" String

	MemberFromUser = "the"? "member" ("from" | "of") User

	MemberFromID = "the"? "member" ("from" | "with" | "of") "id" String

	ChannelFromID = "the"? "channel" ("from" | "with" | "of") "id" String

	RoleFromID = "the"? "role" ("from" | "with" | "of") "id" String

	RoleFromName = "the"? "role" ("from" | "with" | "of") "name" String

	// Conditions
	Condition = IsSet
			| NotSet
			| DiscordCondition
			| Contains
			| DoesntContain
			| Equality
			| Comparison

	Contains = String "has" String
			| String "contains" String
			| String "includes" String
	DoesntContain = String ("doesnt" | "doesn't" | "does not") ("contain" | "include") String
	IsSet = AnyType "exists"
			| AnyType "is set"
	NotSet = AnyType ("doesnt" | "doesn't" | "does not") "exist"
			| AnyType ("isnt" | "isn't" | "is not") "set"

	Equality = Equals | NotEqual
	Equals = AnyType "is" AnyType
			| AnyType "equals" AnyType
			| AnyType "is equal to" AnyType
			| AnyType "==" AnyType
			| AnyType "===" AnyType

	NotEqualIsnt = ("isnt" | "isn't" | "is not") "equal to"?
	NotEqualDoesnt = ("doesnt" | "doesn't" | "does not") "equal"
	NotEqual = AnyType NotEqualIsnt AnyType
			| AnyType NotEqualDoesnt AnyType
			| AnyType "!=" AnyType
			| AnyType "!==" AnyType

	Comparison = GreaterThan
				| LessThan
				| GreaterThanOrEqual
				| LessThanOrEqual

	GreaterThanOptions = "is"? ("greater" | "more" | "higher" | "bigger" | "larger") "than"
	GreaterThan = Comparable GreaterThanOptions Comparable
				| Comparable ">" Comparable
	
	GreaterThanOrEqualOptions = "is"? ("greater" | "more" | "higher" | "bigger" | "larger") "than or" ("equal to" | "equals")
	GreaterThanOrEqual = Comparable GreaterThanOrEqualOptions Comparable
				| Comparable ">=" Comparable
	
	LessThanOptions = "is"? ("less" | "lower" | "smaller") "than"
	LessThan = Comparable LessThanOptions Comparable
				| Comparable "<" Comparable

	LessThanOrEqualOptions = "is"? ("less" | "lower" | "smaller") "than or" ("equal to" | "equals")
	LessThanOrEqual = Comparable LessThanOrEqualOptions Comparable
				| Comparable "<=" Comparable

	DiscordCondition = HasChannelPermission
					| HasPermission
					| HasRole
					| IsBot
					| IsHoisted

	HasChannelPermission = Member "has" "the"? "permission"? "to"? Permission "in" Channel
	HasPermission = Member "has" "the"? "permission"? "to"? Permission
	HasRole = Member "has" "the"? "role"? Role
	IsBot = User "is a bot"
	IsHoisted = Role "is hoisted"
				| Role "displays seperately"

	// Conditionals

	Statement = IfStatement

	If = "if" Boolean Block
    ElseIf = "else" IfStatement
    Else = "else" Block

	IfStatement = If Else -- else
				| If ElseIf* -- elseIf
	// Effects

	Effect = AddRole
			| Ban
			| Kick
			| RemoveRole
			| CreateMessage
			| SendDM
			| SetNickname
			| SetVariable

	SetVariable = "set" "the"? "variable"? variableName "to" AnyType

	WithReason = "with"? "reason"
	ReasonText = (WithReason | "because" | "due to")

	AddRole = "add" "the"? "role"? Role "to" Member -- noReason
			| "add" "the"? "role"? Role "to" Member ReasonText String -- reason

	RemoveRole = "remove" "the"? "role"? Role "from" Member -- noReason
				| "remove" "the"? "role"? Role "from" Member ReasonText String -- reason

	Ban = "ban" "the"? "member"? Member -- noReason
		| "ban" "the"? "member"? Member ReasonText String -- reason

	Kick = "kick" "the"? "member"? Member -- noReason
		| "kick" "the"? "member"? Member ReasonText String -- reason

	CreateMessage = ("create" | "send") "a"? "message"? ("with content" | "saying")? String

	SendDM = "send" ("a" | "an")? ("dm" | "direct message") ("with content" | "saying")? String "to" User

	SetNickname = "set" "the"? "nickname of" Member "to" String
	
	// Loops

	Loop = LoopTimes Block

	LoopIndex = "loop-index" | "loop index"

	LoopTimes = "loop" Integer "times"


	// String literals
	String = StringPriority
			| stringLiteral
			| CleanContent
			| ID
			| FormatDate
			| MemberStatus
			| Mention
			| MessageContent
			| Name
			| GuildIcon
			| GuildRegion
			| Substring
			| Concat
			| Lowercase
			| Uppercase
			| UserDiscriminator
			| UserAvatar
			| UserTag
			| AnyType
			| Variable
	
	stringLiteral = "\"" doubleStringCharacter* "\""
				| "'" singleStringCharacter* "'"
	doubleStringCharacter = ~("\"" | "\\") any -- nonEscaped
							| "\\" escapeSequence -- escaped
	singleStringCharacter = ~("'" | "\\") any -- nonEscaped
							| "\\" escapeSequence -- escaped
	escapeSequence = singleEscapeCharacter | nonEscapeCharacter
	singleEscapeCharacter = "'" | "\"" | "\\" | "b" | "f" | "n" | "r" | "t" | "v"
	nonEscapeCharacter = ~(singleEscapeCharacter | digit | "x" | "u" | lineTerminator) any
	
	// numeric literals
	Number = NumberPriority
    		| Arithmetic
			| Integer
			| numericLiteral
			| AbsoluteValue
			| Difference
			| Variable

	Integer = IntegerPriority
			| integerLiteral
            | CreationTimestamp
            | MemberJoinTimestamp
            | MemberCount
            | IndexOf
			| RandomNumber
			| Rounded
			| RoundedDown
			| RoundedUp
			| Length
			| LoopIndex
			| Variable

	numericLiteral = floatLiteral | integerLiteral
	
	integerLiteral = "-" integerLiteral -- neg
					| integer
	
	floatLiteral = "-" floatLiteral -- neg
				| float

	integer = digit+
	float = digit* "." digit+

	// booleans
	Boolean = BooleanPriority
			| booleanLiteral
			| Condition
			| InvertBoolean
			| And
			| Or
			| Variable

	InvertBoolean = ("not" | "!") Boolean
	And = Boolean ("&&" | "and") Boolean
	Or = Boolean ("||" | "or") Boolean
	booleanLiteral = "true" | "false"

	// permission literal(s)
	Permission = PermissionPriority
				| permissionLiteral
				| Variable

	permissionLiteral = createInvite
						| kickMembers
						| banMembers
						| administrator
						| manageChannels
						| manageGuild
						| addReactions
						| viewAuditLogs
						| readMessages
						| sendMessages
						| sendTTSMessages
						| manageMessages
						| embedLinks
						| attachFiles
						| readMessageHistory
						| mentionEveryone
						| externalEmojis
						| voiceConnect
						| voiceSpeak
						| voiceMuteMembers
						| voiceDeafenMembers
						| voiceMoveMembers
						| voiceUseVAD
						| changeNickname
						| manageNicknames
						| manageRoles
						| manageWebhooks
						| manageEmojis

	createInvite = "create a invite" | "create an invite" | "create invites"
	kickMembers = "kick members"
	banMembers = "ban members"
	administrator = "admin" | "administrator"
	manageChannels = "manage channels"
	manageGuild = "manage guild"
	addReactions = "add reactions"
	viewAuditLogs = "view logs" | "view audit logs"
	readMessages = "read messages" | "read"
	sendMessages = "send messages" | "send"
	sendTTSMessages = "send tts messages" | "send tts" | "send text to speech" | "send text to speech messages"
	manageMessages = "manage messages" | "delete messages"
	embedLinks = "send links" | "embed links"
	attachFiles = "send files" | "attach files"
	readMessageHistory = "read message history" | "read history"
	mentionEveryone = "mention everyone" | "use everyone"
	externalEmojis = "external emojis" | "use external emojis" | "external emotes" | "use external emotes"
	voiceConnect = "voice connect" | "connect to voice" | "connect"
	voiceSpeak = "voice speak" | "speak in voice" | "speak"
	voiceMuteMembers = "voice mute members" | "mute members"
	voiceDeafenMembers = "voice deafen members" | "deafen members"
	voiceMoveMembers = "move members" | "voice move members"
	voiceUseVADOptions =  "voice use" caseInsensitive<"vad"> | "use" caseInsensitive<"vad">
	voiceUseVAD = voiceUseVADOptions | caseInsensitive<"vad">
	changeNickname = "change nickname"
	manageNicknames = "manage nicknames"
	manageRoles = "manage roles"
	manageWebhooks = "manage webhooks"
	manageEmojis = "manage emojis"

	// other types
	User = UserPriority
		| userLiteral
		| Member
		| MessageAuthor
		| GuildOwner
		| UserFromID
		| Variable

	Member = MemberPriority
			| MemberFromID
			| MemberFromUser
			| Variable

	Message = MessagePriority
			| messageLiteral
			| Variable

	Channel = ChannelPriority
			| channelLiteral
			| ChannelFromID
			| Variable

	Role = RoleFromID
			| RoleFromName
			| RolePriority
			| Variable

	Guild = GuildPriority
			| guildLiteral
			| Variable

	userLiteral = "author" | "message sender" | "sender" | "message author"
	messageLiteral = "message" | "sent message"
	channelLiteral = "channel" | "channel from message"
	guildLiteral = "guild" | "server" | "guild from message" | "server from message"

	Variable = VariablePriority | variableLiteral
	variableLiteral = ("value" "of")? variableName
	variableName = "{" alnum+ "}"

	DiscordType = User | Member | Message | Channel | Role | Guild | Variable
	Mentionable = User | Member | Channel | Role | Variable
	HasName = Channel | Role | Guild | Variable
	Comparable = String | Number
}